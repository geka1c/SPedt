
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Движения.Тарифы.Записывать = Истина;
	Движения.ТарифыПоНаправлениям.Записывать = Истина;
	ДвиженияТарифы(Движения.Тарифы);
	ДвиженияТарифыПоНаправлениям(Движения.ТарифыПоНаправлениям);

	
КонецПроцедуры

Процедура ДвиженияТарифы(Движения) 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СинхронизацияТарифовТарифы.Габарит КАК Габарит,
		|	СинхронизацияТарифовТарифы.ЦенаХранения КАК ЦенаХранения,
		|	СинхронизацияТарифовТарифы.СрокХранения КАК СрокХранения,
		|	СинхронизацияТарифовТарифы.Штраф КАК Штраф,
		|	СинхронизацияТарифовТарифы.Заморозка КАК Заморозка,
		|	СинхронизацияТарифовТарифы.Негабарит КАК Негабарит,
		|	СинхронизацияТарифовТарифы.ЦенаЗаКГ КАК ЦенаЗаКГ,
		|	СинхронизацияТарифовТарифы.ЦенаЗаКуб КАК ЦенаЗаКуб
		|ПОМЕСТИТЬ таблицаДокумента
		|ИЗ
		|	Документ.СинхронизацияТарифов.Тарифы КАК СинхронизацияТарифовТарифы
		|ГДЕ
		|	СинхронизацияТарифовТарифы.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Габарит
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТарифыСрезПоследних.Период КАК Период,
		|	ТарифыСрезПоследних.Габарит КАК Габарит,
		|	ТарифыСрезПоследних.ЦенаХранения КАК ЦенаХранения,
		|	ТарифыСрезПоследних.СрокХранения КАК СрокХранения,
		|	ТарифыСрезПоследних.Штраф КАК Штраф,
		|	ТарифыСрезПоследних.Заморозка КАК Заморозка,
		|	ТарифыСрезПоследних.негабарит КАК негабарит,
		|	ТарифыСрезПоследних.ЦенаЗаКГ КАК ЦенаЗаКГ,
		|	ТарифыСрезПоследних.ЦенаЗаКуб КАК ЦенаЗаКуб,
		|	ТарифыСрезПоследних.Отменен КАК Отменен,
		|	ТарифыСрезПоследних.кодТарифа КАК кодТарифа
		|ПОМЕСТИТЬ втТарифыИзРегистра
		|ИЗ
		|	РегистрСведений.Тарифы.СрезПоследних(&НаДату, ) КАК ТарифыСрезПоследних
		|ГДЕ
		|	НЕ ТарифыСрезПоследних.Отменен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Габарит
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТарифыИзРегистра.Габарит КАК Габарит,
		|	втТарифыИзРегистра.ЦенаХранения КАК ЦенаХранения,
		|	втТарифыИзРегистра.СрокХранения КАК СрокХранения,
		|	втТарифыИзРегистра.Штраф КАК Штраф,
		|	втТарифыИзРегистра.Заморозка КАК Заморозка,
		|	втТарифыИзРегистра.негабарит КАК Негабарит,
		|	втТарифыИзРегистра.ЦенаЗаКГ КАК ЦенаЗаКГ,
		|	втТарифыИзРегистра.ЦенаЗаКуб КАК ЦенаЗаКуб,
		|	ИСТИНА КАК Отменен,
		|	втТарифыИзРегистра.Габарит.Код КАК кодТарифа
		|ПОМЕСТИТЬ втОтменить
		|ИЗ
		|	втТарифыИзРегистра КАК втТарифыИзРегистра
		|ГДЕ
		|	НЕ втТарифыИзРегистра.Габарит В
		|				(ВЫБРАТЬ
		|					таблицаДокумента.Габарит КАК Габарит
		|				ИЗ
		|					таблицаДокумента КАК таблицаДокумента)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	таблицаДокумента.Габарит КАК Габарит,
		|	таблицаДокумента.ЦенаХранения КАК ЦенаХранения,
		|	таблицаДокумента.СрокХранения КАК СрокХранения,
		|	таблицаДокумента.Штраф КАК Штраф,
		|	таблицаДокумента.Заморозка КАК Заморозка,
		|	таблицаДокумента.Негабарит КАК Негабарит,
		|	таблицаДокумента.ЦенаЗаКГ КАК ЦенаЗаКГ,
		|	таблицаДокумента.ЦенаЗаКуб КАК ЦенаЗаКуб,
		|	ЛОЖЬ КАК Отменен,
		|	таблицаДокумента.Габарит.Код КАК кодТарифа
		|ПОМЕСТИТЬ Сбор
		|ИЗ
		|	таблицаДокумента КАК таблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТарифыИзРегистра КАК втТарифыИзРегистра
		|		ПО таблицаДокумента.Габарит = втТарифыИзРегистра.Габарит
		|ГДЕ
		|	(втТарифыИзРегистра.Габарит ЕСТЬ NULL
		|			ИЛИ НЕ втТарифыИзРегистра.Габарит ЕСТЬ NULL
		|				И (таблицаДокумента.ЦенаХранения <> втТарифыИзРегистра.ЦенаХранения
		|					ИЛИ таблицаДокумента.СрокХранения <> втТарифыИзРегистра.СрокХранения
		|					ИЛИ таблицаДокумента.Штраф <> втТарифыИзРегистра.Штраф
		|					ИЛИ таблицаДокумента.Заморозка <> втТарифыИзРегистра.Заморозка
		|					ИЛИ таблицаДокумента.Негабарит <> втТарифыИзРегистра.негабарит
		|					ИЛИ таблицаДокумента.ЦенаЗаКГ <> втТарифыИзРегистра.ЦенаЗаКГ
		|					ИЛИ таблицаДокумента.ЦенаЗаКуб <> втТарифыИзРегистра.ЦенаЗаКуб
		|					ИЛИ втТарифыИзРегистра.Отменен))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ПериодДок КАК Период,
		|	втОтменить.Габарит КАК Габарит,
		|	втОтменить.ЦенаХранения КАК ЦенаХранения,
		|	втОтменить.СрокХранения КАК СрокХранения,
		|	втОтменить.Штраф КАК Штраф,
		|	втОтменить.Заморозка КАК Заморозка,
		|	втОтменить.Негабарит КАК Негабарит,
		|	втОтменить.ЦенаЗаКГ КАК ЦенаЗаКГ,
		|	втОтменить.ЦенаЗаКуб КАК ЦенаЗаКуб,
		|	втОтменить.Отменен КАК Отменен,
		|	втОтменить.кодТарифа КАК кодТарифа
		|ИЗ
		|	втОтменить КАК втОтменить
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&ПериодДок,
		|	Сбор.Габарит,
		|	Сбор.ЦенаХранения,
		|	Сбор.СрокХранения,
		|	Сбор.Штраф,
		|	Сбор.Заморозка,
		|	Сбор.Негабарит,
		|	Сбор.ЦенаЗаКГ,
		|	Сбор.ЦенаЗаКуб,
		|	Сбор.Отменен,
		|	Сбор.кодТарифа
		|ИЗ
		|	Сбор КАК Сбор";
	
	Если ЗначениеЗаполнено(Дата) Тогда
		НаДату = Новый Граница(МоментВремени(),ВидГраницы.Исключая);
	Иначе	
		НаДату = ТекущаяДата();
	КонецЕсли;	
	Запрос.Параметры.Вставить("Ссылка",Ссылка);
	Запрос.Параметры.Вставить("ПериодДок",Дата);
	Запрос.Параметры.Вставить("НаДату",НаДату);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() цикл
		новаяЗапись = Движения.Добавить();
		ЗаполнитьЗначенияСвойств(новаяЗапись,Выборка);
	КонецЦикла;	
КонецПроцедуры

Процедура ДвиженияТарифыПоНаправлениям(Движения)
	//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СинхронизацияТарифовТарифыПоНаправлениям.Габарит КАК Габарит,
		|	СинхронизацияТарифовТарифыПоНаправлениям.Откуда КАК Откуда,
		|	СинхронизацияТарифовТарифыПоНаправлениям.Куда КАК Куда,
		|	СинхронизацияТарифовТарифыПоНаправлениям.Стоимость КАК Стоимость
		|ПОМЕСТИТЬ втТаблицаДокумента
		|ИЗ
		|	Документ.СинхронизацияТарифов.ТарифыПоНаправлениям КАК СинхронизацияТарифовТарифыПоНаправлениям
		|ГДЕ
		|	СинхронизацияТарифовТарифыПоНаправлениям.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Габарит,
		|	Откуда,
		|	Куда
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТарифыПоНаправлениямСрезПоследних.Габарит КАК Габарит,
		|	ТарифыПоНаправлениямСрезПоследних.Откуда КАК Откуда,
		|	ТарифыПоНаправлениямСрезПоследних.Куда КАК Куда,
		|	ТарифыПоНаправлениямСрезПоследних.Стоимость КАК Стоимость,
		|	ТарифыПоНаправлениямСрезПоследних.Отменен КАК Отменен
		|ПОМЕСТИТЬ втРегистр
		|ИЗ
		|	РегистрСведений.ТарифыПоНаправлениям.СрезПоследних(&НаДату, ) КАК ТарифыПоНаправлениямСрезПоследних
		|ГДЕ
		|	НЕ ТарифыПоНаправлениямСрезПоследних.Отменен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Габарит,
		|	Откуда,
		|	Куда
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРегистр.Габарит КАК Габарит,
		|	втРегистр.Откуда КАК Откуда,
		|	втРегистр.Куда КАК Куда,
		|	втРегистр.Стоимость КАК Стоимость,
		|	ИСТИНА КАК Отменен
		|ПОМЕСТИТЬ втОтменить
		|ИЗ
		|	втРегистр КАК втРегистр
		|ГДЕ
		|	НЕ (втРегистр.Габарит, втРегистр.Откуда, втРегистр.Куда) В
		|				(ВЫБРАТЬ
		|					втТаблицаДокумента.Габарит КАК Габарит,
		|					втТаблицаДокумента.Откуда КАК Откуда,
		|					втТаблицаДокумента.Куда КАК Куда
		|				ИЗ
		|					втТаблицаДокумента КАК втТаблицаДокумента)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаДокумента.Габарит КАК Габарит,
		|	втТаблицаДокумента.Откуда КАК Откуда,
		|	втТаблицаДокумента.Куда КАК Куда,
		|	втТаблицаДокумента.Стоимость КАК Стоимость,
		|	ЛОЖЬ КАК Отменен
		|ПОМЕСТИТЬ втСбор
		|ИЗ
		|	втТаблицаДокумента КАК втТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ втРегистр КАК втРегистр
		|		ПО втТаблицаДокумента.Габарит = втРегистр.Габарит
		|			И втТаблицаДокумента.Откуда = втРегистр.Откуда
		|			И втТаблицаДокумента.Куда = втРегистр.Куда
		|ГДЕ
		|	(втРегистр.Габарит ЕСТЬ NULL
		|			ИЛИ НЕ втРегистр.Габарит ЕСТЬ NULL
		|				И втТаблицаДокумента.Стоимость <> втРегистр.Стоимость)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОтменить.Габарит КАК Габарит,
		|	&ПериодДок КАК Период,
		|	втОтменить.Откуда КАК Откуда,
		|	втОтменить.Куда КАК Куда,
		|	втОтменить.Стоимость КАК Стоимость,
		|	втОтменить.Отменен КАК Отменен
		|ИЗ
		|	втОтменить КАК втОтменить
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втСбор.Габарит,
		|	&ПериодДок,
		|	втСбор.Откуда,
		|	втСбор.Куда,
		|	втСбор.Стоимость,
		|	втСбор.Отменен
		|ИЗ
		|	втСбор КАК втСбор";
	
	Если ЗначениеЗаполнено(Дата) Тогда
		НаДату = Новый Граница(МоментВремени(),ВидГраницы.Исключая);
	Иначе	
		НаДату = ТекущаяДата();
	КонецЕсли;	
	Запрос.Параметры.Вставить("Ссылка",Ссылка);
	Запрос.Параметры.Вставить("ПериодДок",Дата);
	Запрос.Параметры.Вставить("НаДату",НаДату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() цикл
		новаяЗапись = Движения.Добавить();
		ЗаполнитьЗначенияСвойств(новаяЗапись,Выборка);
	КонецЦикла;	

	
	
КонецПроцедуры	