
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	УстановитьВыдачуВГруппуДоставки(Отказ);

	ДвиженияСтатусыДоставки(Отказ, РежимПроведения) ;
	ДвиженияСтатусыДоставкиСвернуто(Отказ, РежимПроведения);

	ДвиженияСтатусыЗаявокВТК(Отказ, РежимПроведения);
КонецПроцедуры

Процедура ДвиженияСтатусыДоставки(Отказ, Режим)
	Движения.СтатусыДоставки.Записывать = Истина;
	Для Каждого ТекСтрокаПокупки Из Заказы Цикл
		Движение = Движения.СтатусыДоставки.Добавить();
		Движение.Период = Дата;
		Движение.Груз = ТекСтрокаПокупки.Груз;
		Движение.Статус = ?(ВидОперации=Перечисления.ВидыОперацийОтчетОДоставке.Доставлен,Перечисления.СтатусыОтправкиГруза.Доставлен,Перечисления.СтатусыОтправкиГруза.Утрачен);
	КонецЦикла;
КонецПроцедуры

Процедура ДвиженияСтатусыДоставкиСвернуто(Отказ, Режим)
	Движения.СтатусыДоставкиСвернуто.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетОДоставкеЗаказы.Заявка как ЗаявкаВТК,
		|	ВЫБОР
		|		КОГДА ОтчетОДоставкеЗаказы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетОДоставке.Доставлен)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиГруза.Доставлен)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиГруза.Утрачен)
		|	КОНЕЦ КАК Статус,
		|	ОтчетОДоставкеЗаказы.Ссылка.Дата КАК Период,
		|	ОтчетОДоставкеЗаказы.Заявка.КоличествоМест КАК КоличествоМест,
		|	СУММА(ОтчетОДоставкеЗаказы.Груз.Количество) КАК Количество,
		|	СУММА(1) КАК КоличествоГС
		|ИЗ
		|	Документ.ОтчетОДоставке.Заказы КАК ОтчетОДоставкеЗаказы
		|ГДЕ
		|	ОтчетОДоставкеЗаказы.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтчетОДоставкеЗаказы.Заявка,
		|	ВЫБОР
		|		КОГДА ОтчетОДоставкеЗаказы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтчетОДоставке.Доставлен)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиГруза.Доставлен)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиГруза.Утрачен)
		|	КОНЕЦ,
		|	ОтчетОДоставкеЗаказы.Ссылка.Дата,
		|	ОтчетОДоставкеЗаказы.Заявка.КоличествоМест";
	Запрос.Параметры.Вставить("Ссылка",Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Движения.СтатусыДоставкиСвернуто.Загрузить(РезультатЗапроса.Выгрузить());

	
КонецПроцедуры




Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	СПКонтрольДоставкиГрузов.ПроверитьУникальностьСтатусаДляГруза(ЭтотОбъект,Отказ);
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	СПКонтрольДоставкиГрузов.ЗаполнитьЗаявкиПоКоробкам(ЭтотОбъект,"Заказы","Груз");

	
	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОтчетОДоставкеЗаявкиВТК.Заявка,
		|	ОтчетОДоставкеЗаявкиВТК.Ссылка
		|ИЗ
		|	Документ.ОтчетОДоставке.ЗаявкиВТК КАК ОтчетОДоставкеЗаявкиВТК
		|ГДЕ
		|	ОтчетОДоставкеЗаявкиВТК.Ссылка <> &Ссылка
		|	И ОтчетОДоставкеЗаявкиВТК.Заявка В(&СписокЗаявок)
		|	И ОтчетОДоставкеЗаявкиВТК.Ссылка.Проведен
		|	И ОтчетОДоставкеЗаявкиВТК.Ссылка.ВидОперации = &ВидОперации 
		|	И ОтчетОДоставкеЗаявкиВТК.Заявка <> ЗНАЧЕНИЕ(Документ.ЗаявкаВТранспортнуюКомпанию.ПустаяСсылка)";
	Запрос.УстановитьПараметр("СписокЗаявок", ЗаявкиВТК.ВыгрузитьКолонку("Заявка"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидОперации", ВидОперации);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заявка "+ВыборкаДетальныеЗаписи.Заявка+" уже закрыта доокументом "+ВыборкаДетальныеЗаписи.ссылка,,,,Отказ); 
	КонецЦикла;
КонецПроцедуры


Процедура ДвиженияСтатусыЗаявокВТК(Отказ, Режим)
	Движения.СтатусыЗаявокВТК.Записывать = Истина;
	Для каждого стр Из ЗаявкиВТК Цикл
		Если не ЗначениеЗаполнено(стр.Заявка) Тогда продолжить; КонецЕсли;
		Движение = Движения.СтатусыЗаявокВТК.Добавить();
		Движение.Период = Дата;
		Движение.Заявка= стр.Заявка;
		Если ВидОперации=Перечисления.ВидыОперацийОтчетОДоставке.Доставлен Тогда
			Движение.Статус = Перечисления.СтатусыОтправкиГруза.Доставлен;
		Иначе
			Движение.Статус = Перечисления.СтатусыОтправкиГруза.Утрачен;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры



Процедура ЗаполнитьПоСпискуЗаявок(СписокЗаявок) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаВТранспортнуюКомпаниюЗаказы.Груз,
		|	ЗаявкаВТранспортнуюКомпаниюЗаказы.Ссылка КАК Заявка
		|ИЗ
		|	Документ.ЗаявкаВТранспортнуюКомпанию.Заказы КАК ЗаявкаВТранспортнуюКомпаниюЗаказы
		|ГДЕ
		|	ЗаявкаВТранспортнуюКомпаниюЗаказы.Ссылка В(&СписокЗаявок)";
	
	Запрос.УстановитьПараметр("СписокЗаявок", СписокЗаявок);

	РезультатЗапроса = Запрос.Выполнить();
	тз=РезультатЗапроса.Выгрузить();
	Заказы.Загрузить(тз);	
	тз.Свернуть("Заявка");
	ЗаявкиВТК.Загрузить(ТЗ);
КонецПроцедуры	




Процедура ЗаполнитьПоСпискуКоробок(СписокКоробок) Экспорт
	Для каждого стр из СписокКоробок Цикл
		заказ=Заказы.Добавить()	;
		заказ.Груз=стр.Значение.ГД;
	КОнецЦикла;
КонецПроцедуры	


#Область ГруппыДоставки
Процедура УстановитьВыдачуВГруппуДоставки(Отказ)
	
	Для каждого стр из Заказы Цикл
		Если не (ТипЗнч(стр.Груз)=Тип("СправочникСсылка.Коробки") и
			стр.Груз.ВидСтикера=Перечисления.ВидыСтикеров.ГруппаДоставки) Тогда Продолжить КонецЕсли;

	
		Если стр.Груз.ОтчетОДоставке<>Ссылка  Тогда
			Если ЗначениеЗаполнено(стр.Груз.ОтчетОДоставке)   Тогда
				ТекстСообщения="Группа доставки "+стр.Покупка+"уже доставлена  "+стр.Груз.ОтчетОДоставке;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				Продолжить;
			КонецЕсли;
		КонецЕсли;	
		обКоробка=стр.Груз.ПолучитьОбъект();
		обКоробка.ОтчетОДоставке=Ссылка;
		Попытка
			обКоробка.Записать();
		Исключение
		КонецПопытки;
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Для каждого стр из Заказы Цикл
		Если не (ТипЗнч(стр.Груз)=Тип("СправочникСсылка.Коробки") и
			стр.Груз.ВидСтикера=Перечисления.ВидыСтикеров.ГруппаДоставки) Тогда Продолжить КонецЕсли;
		
		обКоробка=стр.Груз.ПолучитьОбъект();
		обКоробка.ОтчетОДоставке=Документы.ОтчетОДоставке.ПустаяСсылка();
		Попытка
			обКоробка.Записать();
		Исключение
			Отказ =Истина;
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
